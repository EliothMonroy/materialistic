plugins {
    id 'com.android.application'
    id 'org.jetbrains.kotlin.android'
    id 'org.jetbrains.kotlin.kapt'
}

android {
    compileSdkVersion 33

    defaultConfig {
        applicationId "io.github.hidroh.materialistic"
        minSdkVersion 28
        targetSdkVersion 33
        versionCode 79
        versionName "3.3"
        buildConfigField "int", "LATEST_RELEASE", "77"
        buildConfigField "String", "GITHUB_TOKEN", "\"\""
        buildConfigField "String", "MERCURY_TOKEN", "\"\""
        proguardFiles getDefaultProguardFile('proguard-android.txt'),
                'proguard-rules.pro',
                'proguard-square.pro',
                'proguard-support.pro',
                'proguard-rx.pro'
        resourceConfigurations += ['en', 'zh-rCN', 'es']
    }

    buildTypes {
        debug {
            minifyEnabled !rootProject.ext.ci
        }
        release {
            minifyEnabled true
            shrinkResources true
        }
    }


    compileOptions {
        sourceCompatibility JavaVersion.VERSION_17
        targetCompatibility JavaVersion.VERSION_17
    }
    lint {
        abortOnError true
        absolutePaths false
        explainIssues false
        htmlReport false
        lintConfig file("${rootProject.getRootDir()}/lint.xml")
        textReport true
        warningsAsErrors true
        xmlReport false
    }
    namespace 'io.github.hidroh.materialistic'

}

ext {
    okHttpVersion = '5.0.0-alpha.9'
    daggerVersion = '1.2.5'
    leakCanaryVersion = '1.6.3'
    retrofitVersion = '2.9.0'
    roomVersion = '2.5.1'
    lifecycleVersion = '2.6.1'
}

dependencies {
    implementation 'androidx.appcompat:appcompat:1.6.1',
            'androidx.recyclerview:recyclerview:1.3.0',
            'androidx.cardview:cardview:1.0.0',
            'androidx.swiperefreshlayout:swiperefreshlayout:1.1.0',
            'com.google.android.material:material:1.9.0',
            'androidx.preference:preference:1.2.0',
            'androidx.browser:browser:1.5.0',
            "com.squareup.dagger:dagger:$daggerVersion",
            "com.squareup.retrofit2:retrofit:$retrofitVersion",
            "com.squareup.retrofit2:converter-gson:$retrofitVersion",
            "com.squareup.retrofit2:adapter-rxjava:$retrofitVersion",
            "com.squareup.okhttp3:okhttp:$okHttpVersion",
            "com.squareup.okhttp3:logging-interceptor:$okHttpVersion",
            "io.reactivex.rxjava2:rxandroid:2.1.1",
            "io.reactivex.rxjava2:rxjava:2.2.21",
            "androidx.room:room-runtime:$roomVersion",
            "androidx.lifecycle:lifecycle-viewmodel-ktx:$lifecycleVersion",
            "androidx.lifecycle:lifecycle-livedata-ktx:$lifecycleVersion",
            "androidx.lifecycle:lifecycle-common-java8:$lifecycleVersion"
            "org.jetbrains.kotlin:kotlin-stdlib-jdk8:$kotlin_version"

    debugImplementation "com.squareup.leakcanary:leakcanary-android${rootProject.hasProperty("leak") ? "" : "-no-op"}:$leakCanaryVersion"
    releaseImplementation "com.squareup.leakcanary:leakcanary-android-no-op:$leakCanaryVersion"
    testImplementation "com.squareup.leakcanary:leakcanary-android-no-op:$leakCanaryVersion"
    kapt "androidx.room:room-compiler:$roomVersion",
            "com.squareup.dagger:dagger-compiler:$daggerVersion"
    kaptTest "androidx.room:room-compiler:$roomVersion",
            "com.squareup.dagger:dagger-compiler:$daggerVersion"
}

apply from: "${rootProject.getRootDir()}/robolectric.gradle"
